plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'ua.beengoo'


def refType = System.getenv("GITHUB_REF_TYPE")
def refName = System.getenv("GITHUB_REF_NAME")
def envVersion = System.getenv("VERSION") ?: System.getenv("RELEASE_VERSION")
def computedVersion = (refType == 'tag' && refName) ? refName : (envVersion ?: 'devbuild')

version = computedVersion

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT")
}

runPaper.folia.registerTask() {
    minecraftVersion("1.21.6")
}

tasks.named('runServer') {
    dependsOn tasks.named('shadowJar')
}

tasks.named('shadowJar') {
    archiveClassifier.set('')
}

tasks.named('jar') {
    enabled = false
}

tasks.named('assemble') {
    dependsOn tasks.named('shadowJar')
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
